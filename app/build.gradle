apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId 'relaxeddd.englishnotify'
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "relaxeddd.englishnotify.infrastructure.AppTestRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            def isInitialized = false

            signingConfigs {
                def keystoreProperties = new Properties()

                try {
                    def keystorePropertiesFile = rootProject.file("keystore.properties")
                    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                    isInitialized = true
                } catch(ignored) {}

                config {
                    if (isInitialized) {
                        keyAlias keystoreProperties['keyAlias']
                        keyPassword keystoreProperties['keyPassword']
                        storeFile file(keystoreProperties['storeFile'])
                        storePassword keystoreProperties['storePassword']
                    }
                }
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isInitialized) {
                signingConfig signingConfigs.config
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout"
    implementation "com.google.android.material:material:$material"

    implementation project(':common')
    implementation project(':common-di')
    implementation project(':preferences')
    implementation project(':view-base')
    implementation project(':common-ui-func')
    implementation project(':notifications')
    implementation project(':domain-words')
    implementation project(':screen-settings')

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    //implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger"

    // TODO: try to use room without android dependencies
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "androidx.work:work-runtime-ktx:$workmanager"

    //In-app rate
    implementation "com.google.android.play:core:$playCore"

    testImplementation "junit:junit:$junit"
    testImplementation "io.mockk:mockk:$mokk"
    testImplementation "androidx.arch.core:core-testing:$archCore"

    androidTestImplementation "androidx.test.ext:junit:$androidJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso"
}
